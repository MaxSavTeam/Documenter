apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.crashlytics'

import java.text.SimpleDateFormat

String projectPath = ""
int buildCode = -1
String globalVersionName = ""

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'

    def buildPropFile = file('build.properties')
    if (buildPropFile.canRead()) {
        Properties p = new Properties()
        p.load(new FileInputStream(buildPropFile))
        buildCode = p['BUILD'].toInteger() + 1
        projectPath = p['PROJECT_PATH']
        p['BUILD'] = buildCode.toString()
        p.store(buildPropFile.newWriter(), null)
    }
    defaultConfig {
        applicationId "com.maxsavitsky.documenter"
        minSdkVersion 24
        
        targetSdkVersion 32
        versionCode 84
        versionName "2.4.3"

        globalVersionName = versionName
        
        buildConfigField "String", "COMPILATION_DATE", "\"${getDate()}\""
        buildConfigField "int", "BUILD_CODE", "${buildCode}"

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true

            resValue "string", "app_name", "Documenter"
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".alpha"
            FirebasePerformance {
                instrumentationEnabled false
            }
            versionNameSuffix '-dev'
            resValue "string", "app_name", "Documenter Dev"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

static String getDate() {
    SimpleDateFormat simpleDateFormat = new SimpleDateFormat("dd/MM/yyyy hh:mm:ss")
    simpleDateFormat.setTimeZone(TimeZone.getTimeZone("GMT+3"))
    return simpleDateFormat.format(new Date())
}

afterEvaluate {
    assembleRelease.doLast {
        exec {
            commandLine("cmd", "/c", "groovy", "${projectPath}app\\after_build_tasks.groovy", 'copyUpdate', globalVersionName)
        }
    }
}

task releaseUpdate {
    doLast {
        exec {
            commandLine("cmd", "/c", "groovy", "${projectPath}app\\after_build_tasks.groovy", 'release', globalVersionName)
        }
    }
}

releaseUpdate.onlyIf {
    System.properties['release'] == "true"
}

task rollbackUpdate(type: Exec) {
    commandLine "cmd", "/c", "groovy", "${projectPath}app\\after_build_tasks.groovy", 'rollBack'
}

dependencies {
    implementation "androidx.work:work-runtime:2.7.1"
    implementation "androidx.concurrent:concurrent-futures:1.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.4.2'
    implementation 'androidx.navigation:navigation-ui:2.4.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.google.android:flexbox:2.0.1'

    implementation 'com.android.support:design:32.0.0'
    implementation 'com.github.clans:fab:1.6.4'

    implementation 'com.github.MaxSavTeam:ExceptionHandler-Library:1.1.2'

    implementation 'com.maxsavteam:updates-checker:1.0.1'
    implementation 'com.maxsavteam.stringalgorithms:string-algorithms:1.0.0'

    implementation 'com.android.support:multidex:1.0.3'

    implementation 'org.jetbrains:annotations:23.0.0'

    implementation 'com.koushikdutta.ion:ion:3.1.0'

    implementation 'com.firebaseui:firebase-ui-auth:8.0.1'
    implementation 'com.google.firebase:firebase-auth:21.1.0'
    implementation "com.google.android.gms:play-services-auth:20.3.0"
    implementation 'com.google.firebase:firebase-perf:20.2.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.3.1'
}