import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {
    signingConfigs {
        release {
            def verfile = file('keystore.properties')
            if (verfile.canRead()) {
                Properties properties = new Properties();
                properties.load(new FileInputStream(verfile))
                storeFile file("${properties['STOREFILE']}")
                storePassword = "${properties['STOREPASS']}"
                keyPassword = "${properties['KEYPASS']}"
                keyAlias = "${properties['KEY']}"
            }
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    def buildPropFile = file('build.properties')
    def code = -1
    if(buildPropFile.canRead()) {
        Properties p = new Properties()
        p.load(new FileInputStream(buildPropFile))
        code = p['BUILD'].toInteger() + 1
        p['BUILD'] = code.toString()
        p.store(buildPropFile.newWriter(), null)
    }

    defaultConfig {
        applicationId "com.maxsavitsky.documenter"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 20
        versionName '0.7'
        buildConfigField "String", "COMPILATION_DATE", "\"${getDate()}\""
        buildConfigField "int", "BUILD_CODE", "${code}"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".alpha"
        }
    }

}

static String getDate() {
    SimpleDateFormat simpleDateFormat = new SimpleDateFormat("dd/MM/yyyy hh:mm:ss");
    simpleDateFormat.setTimeZone(TimeZone.getTimeZone("GMT+3"));
    return simpleDateFormat.format(new Date())
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'com.android.support:design:29.0.0'
    implementation files('libs/quadflask.colorpicker.aar')
}
