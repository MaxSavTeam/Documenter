import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'

String cleanVersionName = ""
int verCode = 0
String projectPath = ""

android {
    signingConfigs {
        release {
            def verfile = file('keystore.properties')
            if (verfile.canRead()) {
                Properties properties = new Properties();
                properties.load(new FileInputStream(verfile))
                storeFile file("${properties['STOREFILE']}")
                storePassword = "${properties['STOREPASS']}"
                keyPassword = "${properties['KEYPASS']}"
                keyAlias = "${properties['KEY']}"
            }
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    def buildPropFile = file('build.properties')
    def code = -1
    if (buildPropFile.canRead()) {
        Properties p = new Properties()
        p.load(new FileInputStream(buildPropFile))
        code = p['BUILD'].toInteger() + 1
        projectPath = p['PROJECT_PATH']
        p['BUILD'] = code.toString()
        p.store(buildPropFile.newWriter(), null)
    }
    defaultConfig {
        applicationId "com.maxsavitsky.documenter"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 29

        verCode = versionCode
        cleanVersionName = "1.4.0" + ".$versionCode" + (code != -1 ? "." + code.toString() : "")

        versionName cleanVersionName + "+${formatDate("yyyyMMddHHmm")}"
        buildConfigField "String", "COMPILATION_DATE", "\"${getDate()}\""
        buildConfigField "int", "BUILD_CODE", "${code}"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            FirebasePerformance {
                instrumentationEnabled true
            }
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".alpha"
            FirebasePerformance {
                instrumentationEnabled false
            }
        }
    }

}

static def String formatDate(String format){
    SimpleDateFormat dateFormat = new SimpleDateFormat(format)
    return dateFormat.format(new Date())
}

static String getDate() {
    SimpleDateFormat simpleDateFormat = new SimpleDateFormat("dd/MM/yyyy hh:mm:ss");
    simpleDateFormat.setTimeZone(TimeZone.getTimeZone("GMT+3"));
    return simpleDateFormat.format(new Date())
}

afterEvaluate{ // delete this if you aren't me
    assembleRelease.doLast{
        exec {
            commandLine "cmd", "/c", "groovy", "${projectPath}app\\after_build_tasks.groovy", 'afterBuildTask', cleanVersionName, verCode
        }
    }
}

task rollbackUpdate(type: Exec){
    commandLine "cmd", "/c", "groovy", "${projectPath}app\\after_build_tasks.groovy", 'rollBack'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.2.1'
    implementation 'androidx.navigation:navigation-ui:2.2.1'
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'com.android.support:design:29.0.0'
    implementation files('libs/quadflask.colorpicker.aar')
    implementation 'com.google.firebase:firebase-core:17.3.0'
    implementation 'com.firebaseui:firebase-ui-auth:6.2.0'
    implementation 'com.google.firebase:firebase-auth:19.3.0'
    implementation 'com.google.firebase:firebase-storage:19.1.1'
    implementation 'com.google.firebase:firebase-database:19.2.1'
    implementation 'com.google.firebase:firebase-perf:19.0.5'

    implementation 'com.rollbar:rollbar-java:1.6.0'
    implementation 'com.rollbar:rollbar-android:1.6.0@aar'
}
